
uniform vec3 eyePos;
uniform float specular_intensity;
uniform float specular_power;

struct BaseLight {
	vec3 color;
	float intensity;
};

struct DirectionalLight {
	BaseLight base;
	vec3 direction;
};

struct Attenuation {
	float constant;
	float linear;
	float exponent;
};

struct PointLight {
	BaseLight base;
	Attenuation attenuation;
	vec3 position;
	float range;
};

struct SpotLight {
	PointLight point_light;
	vec3 direction;
	float cut_off;
};

vec4 calc_light(BaseLight base, vec3 direction, vec3 normal, vec3 worldPos){
	float diffuse_factor = dot(normal, -direction);
	vec4 diffuse_color = vec4(0.0, 0.0, 0.0, 0.0);
	vec4 specular_color = vec4(0.0, 0.0, 0.0, 0.0);
	
	if(diffuse_factor > 0.0){
		diffuse_color = vec4(base.color, 1.0) * base.intensity * diffuse_factor;
		
		// specular calculation
		vec3 direction_to_eye = normalize(eyePos - worldPos);
		
		// phong
		//vec3 reflect_direction = normalize(reflect(direction, normal));
		//float specular_factor = dot(direction_to_eye, reflect_direction);
		
		// blinn-phong (looks better, stretches the sheen more)
		vec3 half_direction = normalize(direction_to_eye - direction);
		float specular_factor = dot(half_direction, normal);
		
		specular_factor = pow(specular_factor, specular_power);
		if(specular_factor > 0.0){
			specular_color = vec4(base.color, 1.0) * specular_intensity * specular_factor;
		}
	}
	
	return diffuse_color + specular_color;
}

vec4 calc_directional_light(DirectionalLight directional_light, vec3 normal, vec3 worldPos){
	return calc_light(directional_light.base, -directional_light.direction, normal, worldPos);
}

vec4 calc_point_light(PointLight point_light, vec3 normal, vec3 worldPos){
	vec3 light_direction = worldPos - point_light.position;
	float distance_to_point = length(light_direction);
	
	if(distance_to_point > point_light.range)
		return vec4(0.0, 0.0, 0.0, 0.0);
	
	light_direction = normalize(light_direction);
	
	vec4 color = calc_light(point_light.base, light_direction, normal, worldPos);
	
	float attenuation =
		point_light.attenuation.constant + 
		point_light.attenuation.linear * distance_to_point + 
		point_light.attenuation.exponent * distance_to_point * distance_to_point +
		0.0001;
		// adding the 0.0001 prevents division to zero
	
	return color / attenuation;
}

vec4 calc_spot_light(SpotLight spot_light, vec3 normal, vec3 worldPos){
	vec3 light_direction = normalize(worldPos - spot_light.point_light.position);
	float spot_factor = dot(light_direction, spot_light.direction);
	
	vec4 color = vec4(0.0, 0.0, 0.0, 0.0);
	
	if(spot_factor > spot_light.cut_off)
		color = calc_point_light(spot_light.point_light, normal, worldPos) * 
				(1.0 - (1.0 - spot_factor) / (1.0 - spot_light.cut_off));

	return color;
}